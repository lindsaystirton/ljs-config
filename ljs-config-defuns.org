#+TITLE: LJS Config Defuns
#+OPTIONS: toc:nil num:nil ^:nil

This file is part of the [[file:ljs-config.org][Lindsay Stirton's Emacs Configurations]]. This
file is all but copy-pased from Kieran Healy's [[https://github.com/kjhealy/emacs-starter-kit/][Emacs Starter-Kit for
the Social Sciences]].

* Starter Kit Defuns
Function definitions


** Network

#+srcname: ljs-config-view-url
#+begin_src emacs-lisp 
  (defun view-url ()
    "Open a new buffer containing the contents of URL."
    (interactive)
    (let* ((default (thing-at-point-url-at-point))
           (url (read-from-minibuffer "URL: " default)))
      (switch-to-buffer (url-retrieve-synchronously url))
      (rename-buffer url t)
      (cond ((search-forward "<?xml" nil t) (xml-mode))
            ((search-forward "<html" nil t) (html-mode)))))
#+end_src

** These belong in coding-hook:

We have a number of turn-on-* functions since it's advised that lambda
functions not go in hooks. Repeatedly evaling an add-to-list with a
hook value will repeatedly add it since there's no way to ensure that
a lambda doesn't already exist in the list.

#+srcname: starter-kit-hook-functions
#+begin_src emacs-lisp 
  (defun local-column-number-mode ()
    (make-local-variable 'column-number-mode)
    (column-number-mode t))
  
  (defun local-comment-auto-fill ()
    (set (make-local-variable 'comment-auto-fill-only-comments) t)
    (auto-fill-mode t))
  
  (defun turn-on-idle-highlight-mode ()
    (idle-highlight-mode t))
      
  (defun turn-on-save-place-mode ()
    (setq save-place t))
  
  (defun turn-on-whitespace ()
    (whitespace-mode t))

#+end_src

*** Make source code prettier
#+begin_src emacs-lisp
(defun ljs-config-pretty-lambdas ()
  (font-lock-add-keywords
   nil `(("(\\(lambda\\>\\)"
          (0 (progn (compose-region (match-beginning 1) (match-end 1)
                                    ,(make-char 'greek-iso8859-7 107))
                    nil))))))
#+end_src

#+srcname: ljs-config-add-local-column-number-mode
#+begin_src emacs-lisp 
(add-hook 'ljs-config-coding-hook 'local-column-number-mode)
#+end_src

#+srcname: ljs-config-add-local-comment-auto-fill
#+begin_src emacs-lisp 
(add-hook 'ljs-config-coding-hook 'local-comment-auto-fill)
#+end_src

#+srcname: ljs-config-add-pretty-lambdas
#+begin_src emacs-lisp
(add-hook 'ljs-config-coding-hook 'ljs-config-pretty-lambdas)
#+end_src
  
#+srcname: run-ljs-config-coding-hook
#+begin_src emacs-lisp 
(defun run-ljs-config-coding-hook ()
  "Enable things that are convenient across all coding buffers."
  (run-hooks 'ljs-config-coding-hook))
#+end_src
\

#+srcname: ljs-config-untabify-buffer
#+begin_src emacs-lisp 
(defun untabify-buffer ()
  (interactive)
  (untabify (point-min) (point-max)))
#+end_src

#+srcname: ljs-config-indent-buffer
#+begin_src emacs-lisp 
(defun indent-buffer ()
  (interactive)
  (indent-region (point-min) (point-max)))
#+end_src

#+srcname: ljs-config-cleanup-buffer
#+begin_src emacs-lisp 
(defun cleanup-buffer ()
  "Perform a bunch of operations on the whitespace content of a buffer."
  (interactive)
;;  (indent-buffer)
  (untabify-buffer)
  (delete-trailing-whitespace))
#+end_src

#+source: cleanup-buffer-on-save
#+begin_src emacs-lisp
  ;; Credit: Alex Ott
  (defun cleanup-buffer-hook ()
    "A hook for cleanup-buffer. cleanup-buffer-modes should be a list
    of modes you want the cleanup-buffer function applied to routinely. "
    (when (member major-mode cleanup-buffer-modes)
      (cleanup-buffer)))
  ;; now files int he modes listed in cleanup-buffer-mode will be
  ;; automatically cleaned every time they are saved. 
  (add-hook 'before-save-hook 'cleanup-buffer-hook)
  
#+end_src

#+source: cleanup-buffer-when
#+begin_src emacs-lisp
  (setq cleanup-buffer-modes 
        '(haskell-mode emacs-lisp-mode lisp-mode scheme-mode
                       ess-mode erlang-mode clojure-mode ruby-mode))  
#+end_src


#+srcname: ljs-config-recentf-ido-find-file
#+begin_src emacs-lisp 
  (defun recentf-ido-find-file () 
    "Find a recent file using Ido." 
    (interactive) 
    (let* ((file-assoc-list 
            (mapcar (lambda (x) 
                      (cons (file-name-nondirectory x) 
                            x)) 
                    recentf-list)) 
           (filename-list 
            (remove-duplicates (mapcar #'car file-assoc-list) 
                               :test #'string=)) 
           (filename (ido-completing-read "Choose recent file: " 
                                          filename-list 
                                          nil 
                                          t))) 
      (when filename 
        (find-file (cdr (assoc filename 
                               file-assoc-list)))))) 
#+end_src

** Other
Useful when a large block of text is required (e.g. for testing)
#+srcname: ljs-config-lorem
#+begin_src emacs-lisp 
(defun lorem ()
  "Insert a lorem ipsum."
  (interactive)
  (insert "Lorem ipsum dolor sit amet, consectetur adipisicing elit, sed do "
          "eiusmod tempor incididunt ut labore et dolore magna aliqua. Ut enim"
          "ad minim veniam, quis nostrud exercitation ullamco laboris nisi ut "
          "aliquip ex ea commodo consequat. Duis aute irure dolor in "
          "reprehenderit in voluptate velit esse cillum dolore eu fugiat nulla "
          "pariatur. Excepteur sint occaecat cupidatat non proident, sunt in "
          "culpa qui officia deserunt mollit anim id est laborum."))
#+end_src


#+source: message-line
#+begin_src emacs-lisp
  (message "LJS Config Defuns loaded.")
#+end_src
